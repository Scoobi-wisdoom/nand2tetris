// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=outAsState, out=plusOne);

    // 여러 Mux 중 Mux16을 써야 한다는 걸 알아야 풀 수 있는 문제다.
    Mux16(a=outAsState, b=plusOne, sel=inc, out=loadIn);
    Mux16(a=loadIn, b=in, sel=load, out=resetIn);
    Mux16(a=resetIn, b=false, sel=reset, out=registerIn);

    // 항상 할당한다는 생각을 해야 푸는 문제다.
    Register(in=registerIn, load=true, out=out, out=outAsState);
}
